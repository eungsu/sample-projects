# 사용자정의 함수 정의하기

## 사용자정의 함수의 형식
```sql
CREATE OR REPLACE FUNCTION 함수명
-- 매개변수 정의, 생략가능
  (매개변수명  IN  타입,
   매개변수명  IN  타입 DEFAULT 기본값)

-- 반환타입 정의
RETURN  반환타입

-- 변수 선언부
IS
  변수명  타입;
  변수명  타입;

-- 수행문의 시작
BEGIN
  수행할 작업(SQL문, IF문, FOR문 등)
  
  RETURN  반환값;

-- 예외처리 정의, 생략가능
  EXCEPTION                                                        
    WHEN  예외명  THEN
      예외발생시 수행할 작업
      
      RETURN  반환값;

--  수행문의 끝
END;
/
```

## 사용자정의 함수 예제

### 두 수를 전달받아서 합계를 반환하는 함수
#### pl/sql 코드
```sql
CREATE OR REPLACE FUNCTION CUSTOM_FN1
    (I_NUM1 IN NUMBER, 
     I_NUM2 IN NUMBER)

RETURN NUMBER

IS
    V_SUM NUMBER;
BEGIN
    V_SUM := I_NUM1 + I_NUM2;

    RETURN V_SUM;
END;
/
```

#### 실행하기
```sql	
SELECT CUSTOM_FN1(10, 20) FROM DUAL;
```

### 사원번호를 전달받아서 연봉을 반환하는 함수
#### pl/sql 코드
```sql
CREATE OR REPLACE FUNCTION CUSTOM_FN2
    (I_EMP_ID IN VARCHAR2)
  
RETURN NUMBER
IS
    V_SALARY NUMBER;
    V_COMMISSION_PCT NUMBER;
    V_ANNUAL_SALARY NUMBER;
BEGIN
    SELECT SALARY, NVL(COMMISSSION_PCT, 0)
    INTO V_SALARY, V_COMMISSION_PCT
    FROM EMPLOYEES
    WHERE EMPLOYEE_ID = I_EMP_ID;

    V_ANNUAL_SALARY := V_SALARY*12 + (V_SALARY*V_COMMISSION_PCT*12);
    
    RETURN V_ANNUAL_SALARY;
END;
/
```

#### 실행하기
```sql
SELECT CUSTOM_FN2(100) FROM DUAL;

SELECT 
  EMPLOYEE_ID,
  FIRST_NAME,
  SALARY,
  CUSTOM_FN2(EMPLOYEE_ID) AS ANNUAL_SALARY
FROM
  EMPLOYEES;
```
------------------------------------------------------------------------------------------------------------------

### 날짜별로 1부터 시작하는 예약번호를 반환하는 함수 정의하기

#### 시퀀스용 테이블 정의하기
```sql
CREATE TABLE CUSTOM_SEQUENCES(	
    SEQ_DATE VARCHAR2(20), 
    SEQ_NAME VARCHAR2(50),
    SEQ_COUNT NUMBER(4,0),
    PRIMARY KEY (SEQ_DATE, SEQ_NAME)
);
```

#### pl/sql 코드
```sql
CREATE OR REPLACE FUNCTION CUSTOM_SEQUENCE
    (I_NAME IN VARCHAR2, 
     I_PATTERN IN VARCHAR2 DEFAULT NULL)
RETURN VARCHAR2
IS
    PRAGMA AUTONOMOUS_TRANSACTION; 
    
    V_COUNT NUMBER;    
    V_CURRENT_DATE VARCHAR2(20);
BEGIN
    IF I_PATTERN IS NULL THEN
        V_CURRENT_DATE := TO_CHAR(SYSDATE, 'YYYYMMDD');
    ELSE
        V_CURRENT_DATE := TO_CHAR(SYSDATE, I_PATTERN);
    END IF;
   
    SELECT SEQ_COUNT
    INTO V_COUNT
    FROM CUSTOM_SEQUENCES
    WHERE SEQ_DATE = V_CURRENT_DATE AND SEQ_NAME = I_NAME;

    V_COUNT := V_COUNT + 1;
    
    UPDATE CUSTOM_SEQUENCES
    SET SEQ_COUNT = V_COUNT
    WHERE SEQ_DATE = V_CURRENT_DATE AND SEQ_NAME = I_NAME;
   
    COMMIT;

    RETURN V_CURRENT_DATE || LPAD(V_COUNT, 4, '0');

    EXCEPTION
        WHEN NO_DATA_FOUND THEN
            V_COUNT := 1;
            INSERT INTO CUSTOM_SEQUENCES (SEQ_DATE, SEQ_COUNT, SEQ_NAME)
            VALUES (V_CURRENT_DATE, V_COUNT, I_NAME);
           
            COMMIT;

            RETURN V_CURRENT_DATE || LPAD(V_COUNT, 4, '0'); 
END;
/
```

#### 사용하기
```sql
SELECT CUSTOM_SEQUENCE('상담신청') FROM DUAL;
-- 패턴의 지정하지 않으면, 'YYYYMMDD000X' 패턴의 값이 반환된다.
-- 202302020001, 202302020002, 202302020003

SELECT CUSTOM_SEQUENCE('예약신청', 'YYYYMMDD') FROM DUAL;
-- 패턴을 지정하면, '패턴형식의날짜000X' 패턴의 값이 반환된다.
-- 202302020001, 202302020002, 202302020003

SELECT CUSTOM_SEQUENCE('예약신청', 'YYMMDD') FROM DUAL;
-- 패턴을 지정하면, '패턴형식의날짜000X' 패턴의 값이 반환된다.
-- 2302020001, 2302020002, 2302020003
```


